<?xml version="1.0" encoding="utf-8"?>
<!--
/*
 * openwms.org, the Open Warehouse Management System.
 *
 * This file is part of openwms.org.
 *
 * openwms.org is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as 
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * openwms.org is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this software. If not, write to the Free
 * Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */
-->
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="init()" dropShadowEnabled="true" showCloseButton="true" close="onCancel(event)" width="342" height="261">

    <mx:Script>
    	<![CDATA[
    		import mx.collections.ArrayCollection;
    		import flash.events.Event;
            import mx.managers.PopUpManager;
            import flash.events.KeyboardEvent;
    		
    		[Bindable]
    		/**
    		 * Bind to a list of available items.
    		 */
    		public var listToDisplay:ArrayCollection;
            [Bindable]
            /**
             * The list of selected items.
             */
            public var selectedItems:ArrayCollection;
            [Bindable]
            /**
             * Text of the dialkogue header.
             */
            public var panelHeaderText:String = "NO TITLE SET";
            [Bindable]
            /**
             * Text of the message hint.
             */
            public var labelText:String = "NO LABEL SET";
            [Bindable]
            /**
             * Used as labelFunction for the list entries.
             */
            public var formatFunction:Function;
            [Bindable]
            /**
             * Text of the submit button.
             */
            public var submitBtnText:String = "Submit";
            [Bindable]
            /**
             * Text of the cancel button.
             */
            public var cancelBtnText:String = "Cancel";
            [Bindable]
            /**
             * Should the dialogue been closed after submit?
             */
            public var closeDialogOnSubmit:Boolean = true;
            /**
             * Do initial things, like setting the focus and title text and register event handlers.
             */
            protected function init():void {
            	this.title = panelHeaderText;
                this.addEventListener(KeyboardEvent.KEY_DOWN, keyEventHandler);
                PopUpManager.centerPopUp(this);
                submitBtn.setFocus();
            }
            /**
             * On form submit, update the list of selectedItems.
             * 
             * @param event A MouseEvent
             */
            public function onSubmit(event:MouseEvent):void {
            	selectedItems = new ArrayCollection(list.selectedItems);
            	if (closeDialogOnSubmit) {
            		onCancel();
            	}
            }
            /**
             * Pressing the cancel button will just close the dialog.
             * 
             * @param event An Event, can be <code>null</code>
             */
            protected function onCancel(event:Event = null):void {
                this.removeEventListener(KeyboardEvent.KEY_DOWN, keyEventHandler);
                PopUpManager.removePopUp(this);
            } 
            /**
             * Binding the CR key to the cancel operation.
             * 
             * @param event A KexboardEvent
             */
            protected function keyEventHandler(event:KeyboardEvent):void {
                if(event.keyCode == 27) { onCancel(); }
            }
    	]]>
    </mx:Script>

    <mx:VBox horizontalAlign="center" verticalAlign="middle" width="100%" height="100%" verticalGap="15">
	    <mx:Label x="56" y="10" text="{labelText}" width="100%" textAlign="center"/>
	    <mx:List id="list" width="95%" height="138" labelFunction="{formatFunction}" allowMultipleSelection="true" dataProvider="{listToDisplay}" alternatingItemColors="[#B7BABC, #FFFFFF]" y="39" x="10"/>
	    <mx:HBox width="100%" horizontalAlign="center">
	        <mx:Button id="submitBtn" label="{submitBtnText}" click="onSubmit(event)" fontSize="12"/>
	        <mx:Button id="cancelBtn" label="{cancelBtnText}" click="onCancel(event)" fontSize="12"/>
	    </mx:HBox>
    </mx:VBox>
</mx:TitleWindow>

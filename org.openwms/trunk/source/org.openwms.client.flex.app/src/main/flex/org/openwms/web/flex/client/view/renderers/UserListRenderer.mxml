<?xml version="1.0" encoding="utf-8"?>
<!--
/*
 * openwms.org, the Open Warehouse Management System.
 *
 * This file is part of openwms.org.
 *
 * openwms.org is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as 
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * openwms.org is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this software. If not, write to the Free
 * Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */
-->
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" showEffect="Fade" width="100%" height="40"
	creationComplete="init()">

	<mx:Script>
	<![CDATA[
    	//import com.af.clientmanager.control.commands.events.clientEvents.GetClientLinksEvent;
    	//import com.af.clientmanager.control.commands.events.clientEvents.GetClientContactsEvent;
    	//import com.adobe.cairngorm.control.CairngormEventDispatcher;
    	import org.openwms.common.domain.system.usermanagement.User;
    	import org.openwms.common.domain.system.usermanagement.UserDetails;
    	import org.openwms.common.domain.system.usermanagement.UserDetails$SEX;
    	import org.openwms.web.flex.client.helper.UserHelper;
    	import mx.effects.Fade;
    	import mx.effects.Blur;
    	import mx.effects.Parallel;
    	import mx.effects.Move;
    	import mx.core.IUIComponent;
    	import mx.managers.DragManager;
    	import mx.core.UIComponent;
    	import mx.core.BitmapAsset;
    	import mx.core.DragSource;
    	import mx.controls.Alert;
		import org.openwms.web.flex.client.model.ModelLocator;

		[Bindable]
		[Embed(source="/assets/images/userDefaultUniSex.jpg")]
       	private var uniSexImage:Class;
		[Bindable]
		[Embed(source="/assets/images/userDefaultMan.jpg")]
       	private var manImage:Class;
		[Bindable]
		[Embed(source="/assets/images/userDefaultWoman.jpg")]
       	private var womanImage:Class;
	  	[Bindable]
	   	public var modelLocator : ModelLocator = ModelLocator.getInstance();
        [Bindable]
        public var userData:User;
        [Bindable]
        public var style:String = "userListItem";

		/**
		 * Initializing the component. Filling all fields with user data.
		 */
        private function init():void {
    	    userFullname.text = userData.fullname;
        	userName.text = userData.username;
        	setUserImage();
        	MoveItem();
        }
        
		/**
		 * Setting the small user preview image depending on the user's sex.
		 */ 
		private function setUserImage():void {
			var asset:BitmapAsset;
			if (UserHelper.haveDetails(userData)) {
				if (userData.userDetails.image != null) {
					img.data = userData.userDetails.image;
					return;
				}
				if (userData.userDetails.sex == UserDetails$SEX.MALE) {
					trace("User has details, setting MAN");
				 	asset = new manImage() as BitmapAsset;
				} else if (userData.userDetails.sex == UserDetails$SEX.FEMALE) {
					trace("User has details, setting FEMALE");
					asset = new womanImage() as BitmapAsset;
				} else {
					trace("User has details, setting UNISEX");
					asset = new uniSexImage() as BitmapAsset;
				}
			} else {
				trace("User has no details, setting Image to UNISEX");
				asset = new uniSexImage() as BitmapAsset;
			}
			img.source=asset;
		}

		/**
		 * This method stores the selected user in the ModelSelector as selectedUser.
		 * It also sends an Event to change settings in the main content panel
		 * When the User has no UserDetails, a new set is instantiated.
		 */
		public function setSelectedItem():void {
			modelLocator.selectedUser = User(this.userData); 
       		dispatchEvent(new Event("userSelectionChanged", true));
		}
		
		/**
		 * Move this item from the left side into the surrounding container.
		 */
        public function MoveItem():void {
			var parallel:Parallel = new Parallel(); 
			parallel.target = userListItem; 
			
			var blur:Blur = new Blur(); 
			blur.blurXFrom = 5; 
			blur.blurXTo = 0; 
			blur.blurYFrom = 5; 
			blur.blurYTo = 0; 
			
			var move:Move = new Move(); 
			move.target = this; 
			move.xFrom = -1000; 
			move.xTo = 5; 
			move.play(); 
			
			parallel.addChild(blur); 				
			parallel.addChild(move); 
			parallel.play();
		}
    ]]>
	</mx:Script>

	<mx:Metadata>
		[Event(name="userSelectionChanged", type="flash.events.Event")]
	</mx:Metadata>

	<mx:HBox id="userListItem" width="95%" height="40" useHandCursor="true" mouseChildren="false" buttonMode="true"
		horizontalScrollPolicy="off" verticalScrollPolicy="off" click="setSelectedItem()" styleName="{style}" horizontalGap="8">
		<mx:Image id="img" height="40" width="40" />
		<mx:VBox width="100%" verticalGap="0" horizontalScrollPolicy="off" verticalScrollPolicy="off" styleName="userListContainer">
			<mx:Label id="userName" styleName="userNameListFont" />
			<mx:Label id="userFullname" styleName="userFullnameListFont" />
		</mx:VBox>
	</mx:HBox>
</mx:Canvas>
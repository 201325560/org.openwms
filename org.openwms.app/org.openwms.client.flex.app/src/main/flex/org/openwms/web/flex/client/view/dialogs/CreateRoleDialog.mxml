<?xml version="1.0" encoding="ISO-8859-1"?>
<!--
/*
 * openwms.org, the Open Warehouse Management System.
 * Copyright (C) 2014 Heiko Scherrer
 *
 * This file is part of openwms.org.
 *
 * openwms.org is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as 
 * published by the Free Software Foundation; either version 2 of the
 * License, or (at your option) any later version.
 *
 * openwms.org is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this software. If not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */
-->
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" title="{I18nUtil.trans(I18nUtil.APP_MAIN, 'txt_create_role_title')}"
 backgroundColor="#F1F07C" creationComplete="init();" height="250" layout="absolute" width="420">

    <mx:Metadata>
        [Name]
        [ManagedEvent(name="ADD_ROLE")]
        [ManagedEvent(name="SAVE_ROLE")]
        [ResourceBundle("appMain")]
    </mx:Metadata>

    <mx:Script>
    	<![CDATA[
            import flash.events.KeyboardEvent;
    		import mx.binding.utils.BindingUtils;
            import mx.managers.PopUpManager;
    		import mx.controls.Alert;
    		import mx.events.ValidationResultEvent;

    		import org.openwms.core.domain.system.usermanagement.Role;
    		import org.openwms.web.flex.client.event.RoleEvent;
            import org.openwms.web.flex.client.model.ModelLocator;
            import org.openwms.web.flex.client.util.I18nUtil;
            
            [Inject]
            [Bindable]
            /**
             * Inject the model.
             */
            public var modelLocator : ModelLocator;
            
            [Bindable]
            /**
             * Use this dialogue to edit an existing Role and pass in the Role to change.
             */
            public var roleToChange : Role;
            
            protected function init() : void {
            	if (roleToChange != null) {
            		createBtn.label = I18nUtil.trans(I18nUtil.APP_MAIN, "btn_save");
            		nameTxt.text = roleToChange.name;
            		descriptionTxt.text = roleToChange.description;
            	}
                nameTxt.setFocus();
                this.addEventListener(KeyboardEvent.KEY_DOWN, keyEventHandler);
                PopUpManager.centerPopUp(this);
            }

            protected function submit() : void {
            	if (stringVal.validate().type == ValidationResultEvent.VALID) {
            		var event:RoleEvent;
            		if (roleToChange == null) {
                        event = new RoleEvent(RoleEvent.ADD_ROLE);
                        var role : Role = new Role(formatRoleName(nameTxt.text), descriptionTxt.text);
                        event.data = role;
                        dispatchEvent(event);
                    }
                    else {
                        nameTxt.text = formatRoleName(nameTxt.text);
                        BindingUtils.bindProperty(roleToChange, "name", nameTxt, "text");
                        BindingUtils.bindProperty(roleToChange, "description", descriptionTxt, "text");
                        event = new RoleEvent(RoleEvent.SAVE_ROLE);
                        event.data = roleToChange;
                        dispatchEvent(event);
                        closeDialog();
                    }
                }
                nameTxt.setFocus();
            }
            
            protected function cancel(event : Event) : void {
                closeDialog(event);
            }            
            
            private function formatRoleName(roleName : String) : String {
            	var result : String = roleName.toUpperCase();
            	if (result.substring(0,Role.PREFIX.length) != Role.PREFIX) {
            		result = Role.PREFIX + result;
            	}
            	return result;
            }

            private function closeDialog(event : Event = null) : void {
                this.removeEventListener(KeyboardEvent.KEY_DOWN, keyEventHandler);
                PopUpManager.removePopUp(this);
            }

            private function keyEventHandler(event : KeyboardEvent) : void {
                if (event.keyCode == 27) {
                   closeDialog(event);
                }
            }
            
            [Observer("ROLE_ADDED")]
            /**
             * When a Role was successfully added reset the form to add a new Role.
             * 
             * @param event unused
             */
            public function onRoleAdded(event : RoleEvent) : void {
            	nameTxt.text = "";
            	descriptionTxt.text = "";
            	nameTxt.setFocus();
            }
    	]]>
    </mx:Script>
    
    <mx:StringValidator id="stringVal" minLength="1" source="{nameTxt}" property="text"/>
    
	<mx:Label x="10" y="10" text="{I18nUtil.trans(I18nUtil.APP_MAIN, 'entity_role_name')}" fontSize="22" textAlign="right"/>
    <mx:TextInput id="nameTxt" width="200" fontSize="22" x="190" y="8" />

	<mx:Label x="10" y="51" text="{I18nUtil.trans(I18nUtil.APP_MAIN, 'entity_role_description')}" fontSize="22" textAlign="right"/>
    <mx:TextArea id="descriptionTxt" y="50" width="200" height="73" fontSize="22"  x="190"/>

	<mx:HBox x="0" y="175" horizontalAlign="center" width="100%">
		<mx:Button id="createBtn" label="{I18nUtil.trans(I18nUtil.APP_MAIN, 'btn_create')}" fontSize="12" click="submit()"/>
		<mx:Button id="cancelBtn" label="{I18nUtil.trans(I18nUtil.APP_MAIN, 'btn_cancel')}" fontSize="12" click="cancel(event)"/>
	</mx:HBox>
	
</mx:TitleWindow>
